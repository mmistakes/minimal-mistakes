---
title:  "[ChunK] 함수요약"

categories:
  - ChunK
tags:
  - [ChunK, Music, function]

toc: true
toc_sticky: true


---

# 소리내기 프로그래밍 기본

`이 포스트는 한양대학교 음악프로그래밍(CSE2020) 도경구 교수님 수업을 참조하였습니다. 개인적으로 공부하면서 참조하려고 만든 포스트입니다.`




## 함수의 선언과 호출
함수는 다른 일반적인 프로그래밍 언어와 비슷하다. 요약(abstraction)은 이름 붙히기 이다. 어떤 사물이나 개념을 알고 있는데 그것을 사용하려면 이름을 붙혀야한다. 우리 주변에 모든 사물들이 이름을 갖고 있는 것과 같다.
프로그램에서 다루는 대상도 이름을 붙혀놓으면 좋다. 그것을 바로 요약이라고 한다.
ChucK 에서 여러번 재사용하고 싶은 코드에 이름을 붙여두고 불러 쓰도록 하는 구조를 **함수(function)** 이라고 한다.

``` java
SinOsc s => dac;

60 => int mynote;
12 +=> mynote;  // 한 옥타브 올려준다.
Std.mtof(mynote) => s.freq;
0.5::second => now;
36 -=> mynote;  // 세 옥타브 내려준다.
Std.mtof(mynote) => s.freq;
0.5::second => now;
24 -=> mynote;  // 두 옥타브 올려준다.
Std.mtof(mynote) => s.freq;
0.5::second => now;
```

옥타브를 내리고 올리고 하는것을 함수로 만들어 두면 편할 것 같다.
정수값 2개를 받아서 한개의 결과값을 내주는 `함수`이다.
``` java
fun int upOctave(int note, int step){
  return note + (step * 12);
}
```

MIDI note 값과 조절할 옥타브를 제공해주면 그것에 맞는 정수를 return 해준다. 위 문제를 함수를 호출하는 식으로 다시 작성해보자
코드의 line은 변함이 없지만 어떤 것을 하고 있는지 의미가 명확하게 나타내어져 있다.
``` java
SinOsc s => dac;

60 => int mynote;
upOctave(mynote, 1) => mynote;  // 한 옥타브 올려준다.
Std.mtof(mynote) => s.freq;
0.5::second => now;

upOctave(mynote, -3) => mynote;  // 세 옥타브 내려준다.
Std.mtof(mynote) => s.freq;
0.5::second => now;

upOctave(mynote, 2) => mynote;  // 두 옥타브 올려준다.
Std.mtof(mynote) => s.freq;
0.5::second => now;
```

## 프로시저 함수
앞 장에서 공부한 무한 루프 코드를 다시 살펴보자.

``` java
SinOsc s => dac;
s.freq() => float mypitch;
0.6 => float mygain;
mygain => s.gain;
<<< s.freq(), s.gain() >>>;

0.5::second => now;
while(true){
  mypitch * 2 => s.freq;
  <<< s.freq(), s.gain() >>>;
  0.5::second => now;
  mypitch => s.freq;
  <<< s.freq(), s.gain() >>>;
  0.5::second => now;
}
```

반복되는 문장을 함수로 만들어주자

``` java
fun showStatus(Osc osc){
  <<< osc.freq(), osc.gain() >>>;
}
```
위 함수를 호출하면 어떠한 오실레이터든, 그 오실레이터의 freq 와 gain 을 print 해준다.
이 함수는 return 할 필요가 없는 함수이다. 이러한 return 이 없는 함수를 프로시저(procedure)라고 따로 구별하여 부른다. 또한 void 라고 명시해준다

이 함수를 이용하여 위 코드를 다음과 같이 다시 작성할 수 있다.
``` java
SinOsc s => dac;
s.freq() => float mypitch;
0.6 => float mygain;
mygain => s.gain;
showStatus(s);

0.5::second => now;
while(true){
  mypitch * 2 => s.freq;
  showStatus(s);
  0.5::second => now;
  mypitch => s.freq;
  showStatus(s);
  0.5::second => now;
}
```

다시 코드를 잘 살펴보면 같은 패턴이 반복되는 부분이 또 존재한다.
어떻게 함수로 만들지 고민해보자.
소리를 내는 함수 makeSound 를 만들어준다. 다음 함수는 소리내는 기능을 맡은 함수이다.
소리를 내려면, 진동기, 주파수, 소리크기 ,진행시간을 인자로 받아야한다.

``` java
fun makeSound(Osc osc, float pitch, float volume, dur length){
  pitch => osc.freq;
  volume => osc.gain;
  showStatus(osc);
  length => now;
}
```
얼마나 소리 낼지에 대한 변수로는 **dur type**으로 지정해주는 것에 주의하자.
이 함수를 이용하여 위 코드를 다음과 같이 다시 작성할 수 있다.

``` java
SinOsc s => dac;
s.freq() => float mypitch;
0.6 => float mygain;
mygain => s.gain;
makeSound(s,mypitch,mygain,0.5::second);

while(true){
  makeSound(s,mypitch*2,mygain,0.5::second);
  makeSound(s,mypitch,mygain,0.5::second);
}
```
훨씬 코드가 깔끔해진것을 확인할 수 있다.
앞 장에서 시도했던 소리 사이 공백 넣기를 이 코드를 가지고 다시 시도해보자. 공백을 넣는 작업도 일정한 패턴을 지니고 있으며, 바뀌는 부분은 대상 진동기와 진행시간이다.
따라서 소리를 일정 시간동안 나지 않게 하는 함수를 다음과 같이 만들 수 있다. 소리가 안난다는 것은 volume이 0 이라는 것이다

```java
fun noSound(Osc osc, dur length){
  0 => osc.gain;
  showStatus(osc);
  length => now;
}
```
위 함수는 length 값 만큼 소리를 안나게 해주는 함수이다.
이 함수를 활용하여 앞 장과 동일한 요령으로 코드를 재작성 해보자

``` java
SinOsc s => dac;
s.freq() => float mypitch;
0.6 => float mygain;
mygain => s.gain;
makeSound(s,mypitch,mygain,0.5::second);
noSound(s,0.1::second);
while(true){
  makeSound(s,mypitch*2,mygain,0.5::second);
  noSound(s,0.1::second);
  makeSound(s,mypitch,mygain,0.5::second);
  noSound(s,0.1::second);
}
```
함수 이름을 정하거나 변수이름을 정하는건 매우 중요하다. method 이름은 CamelCase로 작성하는것이 관례이다.
변수이름은 대문자를 사용하지 않는것이 관례이다 myPitch 보다는 my_pitch 라고 사용하자.

### 함수를 호출하는 두 가지 방법
ChucK 은 함수에 파라미터가 하나만 있는 경우, 함수 호출을 다음과 같이 할 수 있다.
showStatus(osc); 대신 다음과 같이 호출하는 것을 허용한다. osc => showStatus; 
연속적으로 chucking 하는 과정에서 편하다.

## 중복 함수
두개의 발진기로 동시에 소리를 내는 코드를 makeSound 함수를 호출하여 작성해보자.

``` java
SinOsc s => dac;
SinOSc s2 => dac;
s.freq() => float pitch;
s.gain() => float volume;
0.5 *=> volume;
makeSound(s,pitch,volume,second);
makeSound(s2,pitch*3,volume,second);
```
위 코드를 실행시켜보면 소리가 순차적으로 난다 한꺼번에 소리가 나지 않는다. 호출 순서대로 따로 이어서 소리를 낸다.
왜냐하면 호출한 함수가 각각 시간을 진행시켜 소리를 내기 때문이다. 동시에 소리를 내게 하기 위해서는 함수 내부에서 시간을 진행하지 않도록 다음과 같이 함수를 수정해보자.

``` java
fun makeSound(Osc osc, float pitch, float volume, dur length){ // 기존의 makeSound
  pitch => osc.freq;
  volume => osc.gain;
  showStatus(osc);
  length => now;
}

fun makeSound(Osc osc, float pitch, float volume){        // 새로운 makeSound
  pitch => osc.freq;
  volume => osc.gain;
  showStatus(osc);
}

SinOsc s => dac;
SinOSc s2 => dac;
s.freq() => float pitch;
s.gain() => float volume;
0.5 *=> volume;
makeSound(s,pitch,volume);
makeSound(s2,pitch*3,volume);
second => now;
```

이번에는 두개의 pitch의 소리가 한꺼번에 난다. 마찬가지로 세개의 소리를 동시에 발생시킬 수도 있다.
같은 이름의 함수를 두 개 작성했는데, 두 함수가 유사한 기능을 하지만 완전히 똑같지는 않다. 더구나 파라미터의 개수도 다르다. 다행히도 이런 경우를 고려하여
ChucK 프로그래밍 언어는 **함수 이름의 중복(overloading)** 을 허용하고 있다. 파라미터의 개수, 파라미터의 타입이 최소한 하나 다르게 하면 된다.
그러면 프로그래밍 시스템이 파라미터의 개수와 타입을 검사하여 같은 이름의 함수 중에서 가장 적합한 함수를 찾아서 호출해준다.



🌜 주관적인 견해가 담긴 글입니다. 다양한 의견이 있으실 경우
언제든지 댓글 혹은 메일로 지적해주시면 감사하겠습니다! 😄

